# Process with autom4te to create an -*- Autotest -*- test suite.

# AT_CHECK_OPTION(option,output,status)
m4_define([AT_CHECK_OPTION],
[AT_SETUP([intzip $1])
AT_CHECK([intzip $1], [$3], [stdout])
AT_CHECK([fgrep "$2" stdout], [], [ignore])
AT_CLEANUP])
     
# AT_CHECK_ENCODE_DECODE(setup,testfile)
m4_define([AT_CHECK_ENCODE_DECODE],
[AT_SETUP([encode/decode $1])
AT_CHECK([encode-decode $2])
AT_CLEANUP])

AT_INIT([intzip])

# standard options
AT_CHECK_OPTION([--version],[AT_PACKAGE_STRING],[0])
AT_CHECK_OPTION([--help],[Usage],[1])
AT_CHECK_OPTION([--foo],[Usage],[1])

# generated tests
m4_include([gentests.at])

